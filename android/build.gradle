group 'de.mintware.barcode_scan'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.4.31'
    ext.protobuf_version = '0.8.8'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobuf_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.proto.srcDirs += '../protos'
    }
    defaultConfig {
        minSdkVersion 18
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}


protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite { }
            }
        }
    }
}
apply plugin: 'kotlin-android-extensions'
androidExtensions {
    experimental = true
}
dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.31"
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    api 'com.google.android.material:material:1.4.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.annotation:annotation:1.2.0"
    // Barcode model
    implementation 'com.google.mlkit:barcode-scanning:16.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    api("com.google.guava:guava:29.0-jre")
}
